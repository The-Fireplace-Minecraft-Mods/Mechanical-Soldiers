buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Modmuss50"
            url = "http://maven.modmuss50.me/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'ModsIOUpload:ModsIOUpload:+'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.9"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'net.cazzar.gradle.modsio'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version = "1.1.6"
group= "the_fireplace.mechsoldiers"
archivesBaseName = "MechSoldiers-1.12.2"

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "1.12.2-14.23.1.2555"
    runDir = "run"
    replace '${version}', project.version
    mappings = "snapshot_20170815"
	
	clientRunArgs += "--uuid=0b1ec5ad-cb2a-43b7-995d-889320eb2e5b --username=The_Fireplace"
}

dependencies {
	deobfCompile "info.amerifrance.guideapi:Guide-API:1.12-2.1.4-57"
	//deobfCompile "the_fireplace.overlord:Overlord-1.12.1:2.4.6"
	deobfCompile "appeng:applied-llamagistics-1.12.1:rv5-beta-10"
	deobfProvided "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.19-11"
	deobfCompile "mcp.mobius.waila:Hwyla:1.8.22-B37_1.12"
	deobfCompile "blusunrize:ImmersiveEngineering:0.12-+:deobf"
    deobfProvided "slimeknights.mantle:Mantle:1.12-1.3.1.22"
    deobfProvided("slimeknights:TConstruct:1.12-2.8.0.5:deobf") {
        exclude module: "jei_1.12"
    }
}

repositories{
    maven { //JEI, Natura, Mantle, TiCon
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { //Guide API, Waila
		url "http://tehnut.info/maven/"
	}
    /*maven { //Overlord
		url "http://maven.brainstonemod.com/"
	}*/
	maven { //Applied Llamagistics
        url = "http://maven.thiakil.com/"
    }
	maven { //EnderIO, EnderCore, EnderZoo, The One Probe, Chisel
		url = "http://maven.tterrag.com"
	}
	maven { //CoFH Mods
        url = "http://maven.covers1624.net"
    }
    maven { //Immersive Engineering
    	url 'http://blamejared.com/maven'
    }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task showVersion {
    description "Displays the current version"
    group "help"
    
    compileJava.dependsOn showVersion
    deobfCompileDummyTask.dependsOn showVersion
    extractMcpMappings.dependsOn showVersion
  
    doLast {
        def versionFile = new File(buildDir, '.version')
    
        versionFile.getParentFile().mkdirs()
        versionFile.text = version
    }
    
    outputs.upToDateWhen { false }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

curseforge {
  apiKey = project.hasProperty("curseForgeApiKey")? project.curseForgeApiKey : "empty"
  project {
    id = '273117'
    changelog = file('changelog.txt')
    releaseType = 'release'
    addGameVersion '1.12.2'
    addGameVersion '1.12.1'
    addGameVersion '1.12'
	addGameVersion 'Java 8'
    mainArtifact(jar) {
        displayName = jar.archiveName.replace(".jar", "")
        relations{
			requiredLibrary 'overlord'
			optionalLibrary 'applied-llamagistics'
			optionalLibrary 'applied-energistics-2'
			optionalLibrary 'hwyla'
			optionalLibrary 'the-one-probe'
            optionalLibrary 'immersive-engineering'
            optionalLibrary 'tinkers-construct'
		}
    }
    addArtifact(sourceJar) {
        displayName = sourceJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
    addArtifact(deobfJar) {
        displayName = deobfJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
  }
}

modsIO {
    key = project.hasProperty("modsioApiKey")? project.modsioApiKey : "empty"
    project{
		artifact = new File(jar.destinationDir, archivesBaseName+"-"+version+".jar")
		modid = "1270"
		minecraft = "1.12.2"
		try{changelog = file('changelog.txt').text}catch(Exception e){changelog=""}
		tag = ""
		current = true
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging "jar"
                        description "Mechanical-Soldiers"
                        url "https://github.com/The-Fireplace/Mechanical-Soldiers"
                        issueManagement {
                            system "github"
                            url "https://github.com/The-Fireplace/Mechanical-Soldiers/issues"
                        }
                        licenses {
                            license {
                                name "License"
                                url "https://github.com/The-Fireplace/Mechanical-Soldiers/blob/master/LICENSE"
                                distribution "repo"
                            }
                        }
                        developers {
                            developer {
                                id "fireplace"
                                name "The_Fireplace"
                                roles {
                                    role "owner"
                                    role "developer"
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

